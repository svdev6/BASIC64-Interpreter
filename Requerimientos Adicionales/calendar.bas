010 DEF FNM(N) = N - 7*INT(N/7)
020 DEF FNY(Y) = Y +  INT(Y/4) - INT (Y/100) + INT (Y/400)
030 DIM NA$(12), MD(12)

040 FOR I = 1 TO 12
045 READ MD(I)
050 NEXT I

060 LET NA$(1) = "ENERO"
062 LET NA$(2) = "FEBRERO"
064 LET NA$(3) = "MARZO"
066 LET NA$(4) = "ABRIL"
068 LET NA$(5) = "MAYO"
070 LET NA$(6) = "JUNIO"
072 LET NA$(7) = "JULIO"
074 LET NA$(8) = "AGOSTO"
076 LET NA$(9) = "SEPTIEMBRE"
078 LET NA$(10) = "OCTUBRE"
080 LET NA$(11) = "NOVIEMBRE"
082 LET NA$(12) = "DICIEMBRE"

090 PRINT "MES: ";
091 INPUT M
092 PRINT "ANO: ";
093 INPUT Y
100 GOSUB 460
120 LET MD(2) = MD(2) + LY
125 LET LD = MD(M)


130 PRINT NA$(M);"/";Y
132 PRINT "Do Lu Ma Mi Ju Vi Sa"
134 PRINT "--------------------"

136 LET D = 1
138 GOSUB 400

140 REM IMPRIME EL CALENDARIO
145 LET LI$ = ""
150 FOR I = 0 TO DW-1
155   LET LI$ = LI$ + "   "
160 NEXT I
165 PRINT LI$;
170 FOR I = 1 TO LD
172 IF I >= 10 THEN 176
174 PRINT " ";
176 PRINT I;" ";
180 IF FNM(I+DW) <> 0 THEN 200
185 PRINT
200 NEXT I

370 STOP

380 REM DAY OF WEEK 
390 REM CALCULATE ZELLER'S CONGRUENCE.
400 LET A = INT((14 - M)/12)
410 LET M = M + 12*A
420 LET Y = Y - A
430 LET DW = (D + INT((13*M+8)/5) + FNY(Y)) - INT((D + INT((13*M+8)/5) + FNY(Y)) / 7) * 7
440 RETURN

450 REM LEAP YEAR
460 LET LY = 0
465 IF Y - INT(Y / 400) * 400 = 0 THEN 490
470 IF Y - INT(Y / 100) * 100 = 0 THEN 495
475 IF Y - INT(Y / 4) * 4 <> 0 THEN 495
490 LET LY = 1
495 RETURN

600 DATA 31,28,31,30,31,30,31,31,30,31,30,31
650 END
